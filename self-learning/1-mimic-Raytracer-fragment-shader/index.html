<!DOCTYPE html>
<html lang="en">
<head>
  	<style>
		body {
			margin: 0px;
			background-color: #000000;
			overflow: hidden;
		}
	</style>
     <script src = "https://cdnjs.cloudflare.com/ajax/libs/three.js/109/three.min.js">
</script>
	<meta charset="utf-8">
	<meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
	<style>
		body, #container {
			overflow: hidden;
		}
	</style>
</head>
<body>
	<div id="container"></div>
	<script id="vertexShader" type="x-shader/x-vertex">
		void main() { gl_Position = vec4( position, 1.0 ); }

</script>
	<script id="fragmentShader" type="x-shader/x-fragment">	//============================================================
		//PUT YOUR GLSL CODE HERE
		//============================================================

#define PI 3.14159265359

uniform float time;
uniform vec2 mouse;
uniform vec2 resolution;

vec3 repeat(vec3 p, vec3 interval) {
	return mod(p, interval) - interval / 2.0;
}

vec3 translate(vec3 p, vec3 offset) {
	return p - offset;
}

float sphere(vec3 p, float radius) {
	return length(p) - radius;
}

float box(vec3 p, vec3 size) {
	return length(max(abs(p) - size / 2.0, 0.0));
}

float scene(vec3 p) {
	//vec3 q = repeat(p, vec3(2.0, 0.0, 2.0));
	vec3 q = p;
    vec3 sp = translate(q, vec3(-2., 0.0, 0.0));
	vec3 bp = translate(q, vec3(2., 0.0, 0.0));
	return min(sphere(sp, 1.0), box(bp, vec3(1.5)));
}

vec3 normal(vec3 p) {
	float d = 0.00001;
	return normalize(vec3(
		scene(p + vec3(d, 0.0, 0.0)) - scene(p + vec3(- d, 0.0, 0.0)),
		scene(p + vec3(0.0, d, 0.0)) - scene(p + vec3(0.0, - d, 0.0)),
		scene(p + vec3(0.0, 0.0, d)) - scene(p + vec3(0.0, 0.0, - d))
	));
}

// perspective camera
// @param st - sceen position in [0, 1]
// @param position - position of camera
// @param target - target of camera
// @param vup - up direction of camera
// @param vfov - vertical field of view
// @param aspect - aspect ratio (x / y)
// @param origin - ray origin
// @param ray - ray direction
void perspective(in vec2 st, in vec3 position, in vec3 target, in vec3 vup, in float vfov, in float aspect, out vec3 origin, out vec3 ray) {
	vec2 uv = st * 2.0 - 1.0;
	float radian = vfov * PI / 180.0;
	float h = tan(radian * 0.5);
	float w = h * aspect;
	vec3 front = normalize(target - position);
	vec3 right = normalize(cross(front, normalize(vup)));
	vec3 up = normalize(cross(right, front));
	
	ray = normalize(right * w * uv.x + up * h * uv.y + front);	
	origin = position;
}

void main( void ) {
	
	vec2 st = gl_FragCoord.xy / resolution;
    vec2 st2 = st / 2.0;
	vec3 position = vec3(0.,0.,5.);
	//vec3 target = vec3(0.0, 0.0, 0.0);
	vec3 target = vec3((1.0-mouse.x)*2.,(1.0-mouse.y)*2.,0);
	vec3 light = normalize(vec3(1.0, 2.0, 3.0));
	

	vec3 origin, ray;
	perspective(st, position, target, vec3(0.0, 1.0, 0.0), 60.0, resolution.x / resolution.y, origin, ray);
	
	float d = 0.0;
	vec3 p = origin;
	for(int i = 0; i < 64; i++) {
		d = scene(p);
		p += ray * d;
	}
	vec3 color = abs(d) < 0.001 ? clamp(dot(normal(p), light), 0.0, 1.0)  * vec3(1.0, 1.0, 1.0) + vec3(0.2) :  vec3(0.0);
	
	gl_FragColor = vec4(color, 1.0);
	
}





		//============================================================
		//END OF GLSL CODE
		//============================================================

</script>
	<script>
		//change the resolution here. 1 is highest
		var pixel_resolution = 2;
		var container, stats;
		var camera, scene, renderer;
		var uniforms;
		init();
		animate();
		function init() {
			container = document.getElementById('container');
			camera = new THREE.Camera();
			camera.position.z = 1;
			scene = new THREE.Scene();
			var geometry = new THREE.PlaneBufferGeometry(2, 2);
			uniforms = { time: { type: 'f', value: 1.0 }, resolution: { type: 'v2', value: new THREE.Vector2() }, mouse: {type: "v2", value: new THREE.Vector2()}};
			var material = new THREE.ShaderMaterial({ uniforms: uniforms, vertexShader: document.getElementById('vertexShader').textContent, fragmentShader: document.getElementById('fragmentShader').textContent });
			var mesh = new THREE.Mesh(geometry, material);
			scene.add(mesh);
			renderer = new THREE.WebGLRenderer();
			//Hack here to change resolution 
			renderer.setPixelRatio(window.devicePixelRatio / pixel_resolution);
			container.appendChild(renderer.domElement);
			onWindowResize();
			window.addEventListener('resize', onWindowResize, false);
	        window.addEventListener('mousemove', onMouseMove, false);

		}
		function onWindowResize(event) {
			renderer.setSize(window.innerWidth, window.innerHeight);
			uniforms.resolution.value.x = renderer.domElement.width;
			uniforms.resolution.value.y = renderer.domElement.height;

		}
		function animate() {
			requestAnimationFrame(animate);
			render();
		}
		function onMouseMove( event ) {
        	uniforms.mouse.value.x = 2 * ( event.clientX / window.innerWidth );
 	        uniforms.mouse.value.y = 2 * ( 1-(event.clientY) / window.innerHeight
 	        );
        }
		function render() {
			uniforms.time.value += 0.01;
			renderer.render(scene, camera);
		}

</script>
</body>
</html>